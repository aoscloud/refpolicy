## <summary>Utilities for configuring the Linux ethernet bridge.</summary>

########################################
## <summary>
##	Execute a domain transition to run brctl.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`brctl_domtrans',`
	gen_require(`
		type brctl_t, brctl_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, brctl_exec_t, brctl_t)
')

########################################
## <summary>
##  Allow a specified domain to execute, execute without transition,
##  get attributes, map, open, and read the brctl_exec_t file type.
## </summary>
## <param name="domain">
##  <summary>
##  Domain being granted permissions on brctl_exec_t file type.
##  </summary>
## </param>
#
interface(`brctl_exec_permissions',`
    gen_require(`
        type brctl_exec_t;
    ')

    allow $1 brctl_exec_t:file exec_file_perms;
')


########################################
## <summary>
##	Execute brctl in the brctl domain, and
##	allow the specified role the brctl domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`brctl_run',`
	gen_require(`
		attribute_role brctl_roles;
	')

	brctl_domtrans($1)
	roleattribute $2 brctl_roles;
')
